(*************************************************************
 *                                                           *
 *       Cryptographic protocol verifier                     *
 *                                                           *
 *       Bruno Blanchet                                      *
 *                                                           *
 *       Copyright (C) ENS, CNRS, INRIA, 2005-2011           *
 *                                                           *
 *************************************************************)

(*

    Copyright ENS, CNRS, INRIA 
    contributor: Bruno Blanchet, Bruno.Blanchet@ens.fr
    
This software is a computer program whose purpose is to verify 
cryptographic protocols in the computational model.

This software is governed by the CeCILL-B license under French law and
abiding by the rules of distribution of free software.  You can  use, 
modify and/ or redistribute the software under the terms of the CeCILL-B
license as circulated by CEA, CNRS and INRIA at the following URL
"http://www.cecill.info". 

As a counterpart to the access to the source code and  rights to copy,
modify and redistribute granted by the license, users are provided only
with a limited warranty  and the software's author,  the holder of the
economic rights,  and the successive licensors  have only  limited
liability. 

In this respect, the user's attention is drawn to the risks associated
with loading,  using,  modifying and/or developing or reproducing the
software by the user in light of its specific status of free software,
that may mean  that it is complicated to manipulate,  and  that  also
therefore means  that it is reserved for developers  and  experienced
professionals having in-depth computer knowledge. Users are therefore
encouraged to load and test the software's suitability as regards their
requirements in conditions enabling the security of their systems and/or 
data to be ensured and,  more generally, to use and operate it in the 
same conditions as regards security. 

The fact that you are presently reading this means that you have had
knowledge of the CeCILL-B license and that you accept its terms.

*)
(* Version in which the participants only talk to A and B *)

param N.
param N2.
param N3.

(* Bitstrings of size exactly the block size *)
type blocksize [large,fixed].

type nonce [large,fixed].
type host [bounded].
type key [large,fixed].
type keyseed [fixed].

(* Concat1 and concat2 already include the necessary padding to reach the block size *)
fun concat1(nonce, nonce, host, host):blocksize [compos].
fun concat2(nonce, key):blocksize [compos].

forall y:nonce,z:nonce,t:host,u:host,y2:nonce,z2:key;
	(concat1(y,z,t,u) <> concat2(y2,z2)).

(* Shared-key encryption (Block cipher) *)

proba Penc.

expand SPRP_cipher(keyseed, key, blocksize, kgen, enc, dec, Penc).

(* Queries *)

query secret1 keyA.
query secret1 keyB.
query secret keyA.
query secret keyB.

channel c1, c2, c3, c4, c5, c6, c7, c8, c9, start, finish.
const A : host.
const B : host.

event endA(host, host, nonce, key).
event endB(host, host, nonce, key).
event beginA(host, host, nonce).
event beginB(host, host, nonce).

query x:host, y:host, n:nonce, k:key; 
	event endA(x,y,n,k) ==> beginB(x,y,n).
query x:host, y:host, n:nonce, k:key; 
	event endB(x,y,n,k) ==> beginA(x,y,n).
query x:host, y:host, n:nonce, k:key; 
	event inj:endA(x,y,n,k) ==> inj:beginB(x,y,n).
query x:host, y:host, n:nonce, k:key; 
	event inj:endB(x,y,n,k) ==> inj:beginA(x,y,n).


let processA = 
	in(start, ());
	new M : nonce;
	new Na : nonce;
	event beginA(A, B, M);
	let ea1 = enc(concat1(Na, M, A, B), Kas) in
	out(c1, (M, A, B, ea1));
	in(c2, (=M, ea2: blocksize));
	let concat2(=Na, k) = dec(ea2, Kas) in
	event endA(A, B, M, k);
	let keyA:key = k.

let processB =
	in(c3, (M: nonce, =A, =B, ea:blocksize));
	new Nb : nonce;
	event beginB(A, B, M);
	let e2 = enc(concat1(Nb, M, A, B), Kbs) in
	out(c4, (M, A, B, ea, e2));
	in(c5, (=M, ea2: blocksize, eb2: blocksize));
	let concat2(=Nb, k) = dec(eb2, Kbs) in
	out(c6, (M, ea2));
	in(finish, ());
	event endB(A, B, M, k);
	let keyB:key = k.

let processS =
	in(c7, (M: nonce, =A, =B, ea1: blocksize, eb1: blocksize));
	let concat1(Na, =M, =A, =B) = dec(ea1, Kas) in
	let concat1(Nb, =M, =A, =B) = dec(eb1, Kbs) in
	new k: key;
	let e1 = enc(concat2(Na, k), Kas) in
	let e2 = enc(concat2(Nb, k), Kbs) in
	out(c8, (M, e1, e2)).
	
process 
	in(start, ());
	new rKas: keyseed;
	let Kas = kgen(rKas) in
	new rKbs: keyseed;
	let Kbs = kgen(rKbs) in
	out(c9, ());
	((! N processA) |
	 (! N processB) | 
	 (! N processS))
