(*************************************************************
 *                                                           *
 *       Cryptographic protocol verifier                     *
 *                                                           *
 *       Bruno Blanchet                                      *
 *                                                           *
 *       Copyright (C) ENS, CNRS, INRIA, 2005-2011           *
 *                                                           *
 *************************************************************)

(*

    Copyright ENS, CNRS, INRIA 
    contributor: Bruno Blanchet, Bruno.Blanchet@ens.fr
    
This software is a computer program whose purpose is to verify 
cryptographic protocols in the computational model.

This software is governed by the CeCILL-B license under French law and
abiding by the rules of distribution of free software.  You can  use, 
modify and/ or redistribute the software under the terms of the CeCILL-B
license as circulated by CEA, CNRS and INRIA at the following URL
"http://www.cecill.info". 

As a counterpart to the access to the source code and  rights to copy,
modify and redistribute granted by the license, users are provided only
with a limited warranty  and the software's author,  the holder of the
economic rights,  and the successive licensors  have only  limited
liability. 

In this respect, the user's attention is drawn to the risks associated
with loading,  using,  modifying and/or developing or reproducing the
software by the user in light of its specific status of free software,
that may mean  that it is complicated to manipulate,  and  that  also
therefore means  that it is reserved for developers  and  experienced
professionals having in-depth computer knowledge. Users are therefore
encouraged to load and test the software's suitability as regards their
requirements in conditions enabling the security of their systems and/or 
data to be ensured and,  more generally, to use and operate it in the 
same conditions as regards security. 

The fact that you are presently reading this means that you have had
knowledge of the CeCILL-B license and that you accept its terms.

*)
(*

WooLam shared-key auth protocol (1992, computer)
Corrections of Gordon, Jeffrey CSFW'01

A -> B : A
B -> A : N (fresh)
A -> B : { m3, B, N }_kAS
B -> S : A,B,{ m3, B, N }_kAS
S -> B : { m5, A, N }_kBS
B: verify { m5, A, N }_kBS = the message from S

Correct.
*)

(* In order to determine the proof, use interactive mode: 
   set interactiveMode = true.
The proof is as follows: *)

proof {
SArename Rkey;
crypto enc rKbs;
crypto enc rKas;
SArename "@3_r2_67";
success;
auto
}

param N.
param N2.
param N3.

(* Bitstrings of size exactly the block size *)
type blocksize [large,fixed].

type nonce [large,fixed].
type host [bounded].
type key [fixed].
type keyseed [fixed].
type tag [bounded].


fun concat1(tag, host, nonce):blocksize [compos].

(* Shared-key encryption (Block cipher) *)

proba Penc.

expand SPRP_cipher(keyseed, key, blocksize, kgen, enc, dec, Penc).

(* Queries *)

channel c1, c2, c3, c4, c5, c6, c7, c8, c9, c10, c11, c12, c13,
	start, finish.

const A: host.
const B: host.

const tag3: tag.
const tag5: tag.

event beginA(host).
event beginAfull(host, host, nonce).
event endB(host, host, nonce).

query x:host, y:host, z:nonce;
	event endB(x,y,z) ==> beginAfull(x,y,z).
query x:host, y:host, z:nonce;
	event endB(x,y,z) ==> beginA(x).
query x:host, y:host, z:nonce;
	event inj:endB(x,y,z) ==> inj:beginAfull(x,y,z).
query x:host, y:host, z:nonce;
	event inj:endB(x,y,z) ==> inj:beginA(x).

let processA =
	in(c1, hostB2:host); (* Choose the B host *)
	event beginA(hostB2);
	out(c2, A);
	in(c3, n:nonce);
	event beginAfull(hostB2, A, n);
	out(c4, enc(concat1(tag3, hostB2, n), kAS)).

let processB =
	in(c5, hostA2:host);
	new N:nonce;
	out(c6, N);
	in(c7, m:blocksize);
	out(c8, (hostA2, B, m));
	in(c9, m2:blocksize);
	if concat1(tag5, hostA2, N) = dec(m2, kBS) then
	(* OK *)
	if hostA2 = A then
	event endB(B, hostA2, N).

let processK =
	in(c10, (Khost: host, Kkey: key));
	let Rkey:key = 
		if Khost = A then kAS else
		if Khost = B then kBS else
		Kkey.

let processS =
	in(c11, (hostA1:host, hostB0:host, m:blocksize));
	find jB <= N2 suchthat defined(Rkey[jB], Khost[jB]) && (hostB0 = Khost[jB]) then
	let kbs = Rkey[jB] in
	find jA <= N2 suchthat defined(Rkey[jA], Khost[jA]) && (hostA1 = Khost[jA]) then
	let kas = Rkey[jA] in
	let concat1(=tag3, =hostB0, n) = dec(m, kas) in
	out(c12, enc(concat1(tag5, hostA1, n), kbs)).


process 
	in(start, ());
	new rKas: keyseed;
	let kAS = kgen(rKas) in
	new rKbs: keyseed;
	let kBS = kgen(rKbs) in
	out(c13, ());
	((! N processA) |
	 (! N processB) | 
	 (! N processS) |
	 (! N2 processK))
